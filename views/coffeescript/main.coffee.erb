$ = jQuery

$('#consoleOutput').hide()

Renderer = (canvas, handler) ->
  util = CanvasUtil
  canvas = $(canvas).get(0)
  ctx = canvas.getContext '2d'
  particleSystem = null
  objectHandler = handler
  view = null
  keyFilter = null

  init: (system) ->
    particleSystem = system
    particleSystem.screenSize(canvas.width, canvas.height)
    particleSystem.screenPadding(80)
    @initMouseHandling()
    view = @defaultView

  defaultView: (data) ->
    text = []
    usedKeys = {first: true} 
    if keyFilter?.length > 0
      for regex in keyFilter
        for key, value of data
          if key.match(regex)
            text.push "#{value} (#{key})" unless usedKeys[key]
            usedKeys[key] = true
    else
      for key, value of data
        text.push "#{value} (#{key})" unless key is 'first'
      text = text[0..10]
    line[0..28] for line in text

  setKeyFilter: (keyFilterString) ->
    if keyFilterString?.trim() is ''
      keyFilter = null 
    else
      string = "_neo_id, #{keyFilterString}"
      keyFilter = (new RegExp(key.trim()) for key in string.split(','))

  redraw: ->
    ctx.fillStyle = 'white'
    ctx.fillRect 0, 0, canvas.width, canvas.height
    
    particleSystem.eachNode (node, point) =>
      @drawNode(node, point)

    particleSystem.eachEdge (edge, fromPoint, toPoint) =>
      @drawEdge(edge, fromPoint, toPoint)

  drawNode: (node, point) ->
      MARGIN = 10
      
      nodeView = view node.data
      ctx.font = "10pt Times"
      {width, height, count} = util.textSize ctx, nodeView
      width = Math.max(height, 160)
      height = Math.max(height, 60)
      
      color = if node.data.first then "blue" else "green"
      util.roundRect(ctx, point, width+(MARGIN*2), height, color, 10)

      ctx.fillStyle ='white'
      x = util.centerToEdge(point.x, width) 
      y = util.centerToEdge(point.y, height) + MARGIN*2
      util.drawText(ctx, nodeView, x, y)

    
  drawEdge: (edge, fromPoint, toPoint) ->
    ctx.strokeStyle = 'rgba(0,0,0, .333)'
    util.line ctx, fromPoint, toPoint
    
    x = (fromPoint.x + toPoint.x) / 2 - 30
    y = (fromPoint.y + toPoint.y) / 2 
    ctx.font = "10pt Times"
    ctx.fillStyle ='red'
    util.drawText(ctx, [edge.data.rel_type], x, y) 
      
  initMouseHandling: ->
    handler =
      dblclick: (e) ->
        pos = $(canvas).offset()
        _mouseP = arbor.Point(e.pageX-pos.left, e.pageY-pos.top)
        object = particleSystem.nearest(_mouseP)
        objectHandler.activated object.node.name
        false
      
      click: (e) ->
        pos = $(canvas).offset()
        _mouseP = arbor.Point(e.pageX-pos.left, e.pageY-pos.top)
        object = particleSystem.nearest(_mouseP)
        objectHandler.selected object.node.name
        false
    $(canvas)
      .dblclick(handler.dblclick)
      .click(handler.click)


class Space
  constructor: (@sys, nodeCount) ->
    @nodes = {}
    @rels = {}
    @nodeCount = nodeCount
    @allNodes = null
    @allRels = null

  addData: (data) ->
    @allNodes = data.nodes
    @allRels = data.rels
    @refresh()

  refresh: ->
    @clear()
    @addNodes @allNodes
    @addRels @allRels

  clear: ->
    for key, node of @nodes
      @sys.pruneNode(key)
    @nodes = {}
    @rels = {}

  getSelectedNode: ->
    @selectedNode

  setFilter: (filter) ->
    @filter = if filter then new RegExp(filter, 'i') else null

  setNodeCount: (n) ->
    @nodeCount = n

  addNodes: (nodes) ->
    return if nodes.length == 0

    inspect = (node) ->
      name = (for key, value of node.data
        "#{key}:#{value}").join(' ')
      name

    @selectedNode = nodes[0]
    @selectedNode.data.first = true

    filteredNodes = nodes.slice(0)
    filteredNodes = (node for node in filteredNodes when inspect(node).match(@filter)) if @filter
    return if filteredNodes.length is 0
    return if filteredNodes[0] is @selectedNode and filteredNodes.length is 1
    filteredNodes.unshift(@selectedNode)

    filteredNodes = filteredNodes[0...@nodeCount] if filteredNodes.length > @nodeCount
    for node in filteredNodes
      @sys.addNode(node.id, node.data) unless @nodes[node.id]
      @nodes[node.id] = node

  addRels: (rels) ->
    for rel in rels
      if @nodes[rel.start_node] and @nodes[rel.end_node]
        @sys.addEdge(rel.start_node, rel.end_node, rel.data)
        @rels[rel.id] = rel
  
  node: (id) ->
    @nodes[id]

initFormListeners= (space, renderer, evalCode) ->
  $('#loadForm').submit  (e) ->
    e.preventDefault()
    console.log 'load'
    evalCode()

initEventSubscribers = (eventBroker, appContext, space, renderer, getData) ->
  eventBroker.subscribe('nodeCountChanged', ->
    space.setNodeCount appContext.getNodeCount()
  )

  eventBroker.subscribe('nodeFilterChanged', ->
    space.setFilter appContext.getNodeFilter()
  )

  eventBroker.subscribe('keyFilterChanged', ->
    renderer.setKeyFilter appContext.getKeyFilter()
  )

  eventBroker.subscribe('nodeDataChanged', ->
     space.addData appContext.getNodeData()
  )

  eventBroker.subscribe('activatedNodeIdChanged', ->
     getData appContext.getActivatedNodeId()
  )

  eventBroker.subscribe('refresh', ->
    space.refresh()
  )

$ ->

  sys = arbor.ParticleSystem(1000, 600, 0.5) # create the system with sensible repulsion/stiffness/friction
  sys.parameters({gravity:true}) # use center-gravity to make the graph settle nicely (ymmv)

  space = new Space(sys, @appContext.getNodeCount())
 
  
  appendToConsole = (text) ->
      $('#console').val($('#console').val()+ '\n#' + text)

  setError = (text) ->
      $('#consoleOutputArea').val(text)

  getData = (id=0) =>
    depth = $('#depth').val()
    code = if id is 0 then "node = Neo4j.ref_node" else "node = Node._load(#{id})"
    code += "; viz node"
    innerEvalCode(code, depth)

  evalCode = =>
    code = $('#console').val()
    depth = $('#depth').val()
    innerEvalCode(code, depth)

  innerEvalCode = (code, depth) =>
    appContext = @appContext
    console.log code
    $.getJSON "<%= root_url() %>/eval", {code: code, depth: depth}, (data) ->
      if data.result
        $('#consoleOutput').show()
        setError data.result
      else
        setError ""
        $('#consoleOutput').hide()
        appContext.setNodeData data

  activateNode = (id=0) =>
    @appContext.setActivatedNodeId(id)


  showDetails = (id=0) =>
    node = space.node(id)
    return unless node
    html = for key, value of node.data
      if key is 'first' then '' else "<tr><td>#{key}</td><td>#{value}</td></tr>"

    $('#details').empty().append(html.join('\n'))
    #appendToConsole "node = Neo4j::Node.load(#{id})"


  objectHandler =
    activated: activateNode
    selected: showDetails



  sys.renderer = Renderer("#viewport", objectHandler) 

  initFormListeners(space, sys.renderer, evalCode)
  initEventSubscribers(@eventBroker, @appContext, space, sys.renderer, getData)
  getData()